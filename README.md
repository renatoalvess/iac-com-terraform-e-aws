# IAC com Terraform e AWS

Neste repositÃ³rio, vamos aprender a usar o Terraform e a AWS para criar a nossa primeira infra na nuvem.

Este documento tambÃ©m estÃ¡ disponÃ­vel em [formato PDF](docs/README.pdf) e [formato HTML](docs/README.html) para que vocÃª possa visualizÃ¡-lo offline.

## Tabela de conteÃºdos

- [PrÃ©-requisitos](#prÃ©-requisitos)
- [Passo a passo](#passo-a-passo)
- [Erros conhecidos](#erros-conhecidos)
- [Saiba mais](#saiba-mais)

## PrÃ©-requisitos

- InstalaÃ§Ã£o do Terraform
    - https://developer.hashicorp.com/terraform/downloads?product_intent=terraform
    - **Usa Windows?** acesse esse [documento](docs/Como%20Instalar%20o%20Terraform%20no%20Windows.pdf) 
- InstalaÃ§Ã£o do AWS CLI
    - https://docs.aws.amazon.com/cli/latest/userguide/getting-started-install.html
- Criando usuÃ¡rio na AWS
    - [**Acesse esse documento**](docs/CriaÃ§Ã£o%20de%20usuÃ¡rio%20na%20AWS%20e%20chave%20de%20acesso%20para%20vincular%20ao%20AWS%20CLI%20na%20sua%20maquina.pdf) 


## Passo a passo 

Vamos comeÃ§ar a diversÃ£o! ğŸ¥³

1. Comece fazendo o clone do repositÃ³rio:
    > [!TIP]
    > Se vocÃª preferir usar o Github Ã© sÃ³ trocar a URL do repositÃ³rio para `https://github.com/avanti-dvp/iac-com-terraform-e-aws.git`

    ```bash
    git clone https://gitlab.com/avanti-dvp/iac-com-terraform-e-aws.git
    cd iac-com-terraform-e-aws
    ```

    > [!NOTE]
    > Se vocÃª nÃ£o tem o Git instalado ou nÃ£o sabe usÃ¡-lo, sem problema algum, vocÃª pode simplesmente fazer o [download do repositÃ³rio](https://gitlab.com/avanti-dvp/iac-com-terraform-e-aws/-/archive/main/iac-com-terraform-e-aws-main.zip) e descompactÃ¡-lo em sua pasta/diretÃ³rio de trabalho ou na pasta/diretÃ³rio de seu usuÃ¡rio

2. Vamos abrir o Visual Studio Code no diretÃ³rio do repositÃ³rio:
    ```bash
    code .
    ```

3. Dentro do Visual Studio Code, crie um arquivo chamado `provider.tf`, incluindo esse trecho abaixo nele:
    ```hcl
    terraform {
        required_providers {
            aws = {
                source  = "hashicorp/aws"
                version = "~> 5.0"
            }
        }
    }
    
    provider "aws" {
        region = "us-east-1"
    }
    ```

    > [!TIP]
    > O arquivo `provider.tf` Ã© o arquivo que define o provedor que serÃ¡ usado para criar a infraestrutura na nuvem, nesse caso, a AWS.
    > Este arquivo Ã© uma convenÃ§Ã£o entre os desenvolvedores, ele Ã© opcional, mas Ã© uma boa prÃ¡tica ter ele.

4. Agora vamos criar o arquivo `key_pair.tf`, incluindo esse trecho abaixo nele:
    ```hcl
    # Gera uma chave privada RSA de 4096 bits
    resource "tls_private_key" "rsa_key" {
        algorithm = "RSA"
        rsa_bits  = 4096
    }

    # Cria o Key Pair na AWS usando a chave pÃºblica gerada
    resource "aws_key_pair" "ec2_key_pair" {
        key_name   = "ec2-instance-key" # Nome do key pair na AWS
        public_key = tls_private_key.rsa_key.public_key_openssh
    }

    # Salva a chave privada em um arquivo local
    resource "local_file" "private_key_pem" {
        content  = tls_private_key.rsa_key.private_key_pem
        filename = "${path.module}/ec2-instance-key.pem"

        # Define as permissÃµes do arquivo para que apenas o proprietÃ¡rio possa ler e escrever
        file_permission = "0600"
    }
    ```

    > [!TIP]
    > O arquivo `keypair.tf` Ã© o arquivo que define a criaÃ§Ã£o de um par de chaves na AWS.

5. Agora vamos criar o arquivo `security_group.tf`, incluindo esse trecho abaixo nele:
    ```hcl
    # 1. Security Group para liberar a porta 80 (HTTP) para qualquer origem
    resource "aws_security_group" "http_sg" {
        name        = "allow-http-sg"
        description = "Allow HTTP inbound traffic"

        ingress {
            from_port   = 80
            to_port     = 80
            protocol    = "tcp"
            cidr_blocks = ["0.0.0.0/0"]
        }

        tags = {
            Name = "allow-http"
        }
    }

    # 2. Security Group para liberar a porta 22 (SSH) para um IP especÃ­fico
    resource "aws_security_group" "ssh_sg" {
        name        = "allow-ssh-sg"
        description = "Allow SSH inbound traffic from a specific IP"

        ingress {
            from_port   = 22
            to_port     = 22
            protocol    = "tcp"
            cidr_blocks = var.meu_ip_publico
        }

        tags = {
            Name = "allow-ssh"
        }
    }

    # 3. Security Group para liberar todo o trÃ¡fego de saÃ­da (Egress)
    resource "aws_security_group" "egress_all_sg" {
        name        = "allow-all-egress-sg"
        description = "Allow all outbound traffic"

        egress {
            from_port   = 0
            to_port     = 0
            protocol    = "-1" # "-1" representa todos os protocolos
            cidr_blocks = ["0.0.0.0/0"]
        }

        tags = {
            Name = "allow-all-egress"
        }
    }
    ```

    > [!TIP]
    > O arquivo `security_group.tf` Ã© o arquivo que define a criaÃ§Ã£o dos security groups na AWS.

6. Agora vamos criar o arquivo `data.tf`, incluindo esse trecho abaixo nele:
    ```hcl
    data "aws_ami" "amazon_linux" {
        most_recent = true
        owners      = ["amazon"]

        filter {
            name   = "name"
            values = ["amzn2-ami-hvm-*-x86_64-gp2"]
        }
    }
    ```

    > [!TIP]
    > O arquivo `data.tf` Ã© o arquivo que define um datasource responsÃ¡vel por buscar o id da imagem (AMI) mais recente do Amazon Linux 2.

7. Agora vamos criar o arquivo `ec2.tf`, incluindo esse trecho abaixo nele:
    ```hcl
    # Cria a instÃ¢ncia EC2
    resource "aws_instance" "web_server" {
        ami           = data.aws_ami.amazon_linux.id
        instance_type = "t2.micro"
        user_data     = base64encode(file("user_data.sh"))

        # Define o key pair para a instÃ¢ncia
        key_name      = aws_key_pair.ec2_key_pair.key_name

        # Associa os 3 Security Groups Ã  instÃ¢ncia
        vpc_security_group_ids = [
            aws_security_group.http_sg.id,
            aws_security_group.ssh_sg.id,
            aws_security_group.egress_all_sg.id
        ]

        tags = {
            Name = "WebServer-SRE"
        }
    }
    ```

    > [!TIP]
    > O arquivo `ec2.tf` Ã© o arquivo que define a criaÃ§Ã£o da instÃ¢ncia EC2 na AWS.

8. Agora vamos criar o arquivo `outputs.tf`, incluindo esse trecho abaixo nele:
    ```hcl
    # Bloco para exibir o IP pÃºblico da instÃ¢ncia apÃ³s a criaÃ§Ã£o
    output "instance_public_ip" {
      description = "IP pÃºblico da instÃ¢ncia EC2"
      value       = aws_instance.web_server.public_ip
    }

    output "website_url" {
        description = "URL do site provisionado."
        value       = "http://${aws_instance.web_server.public_ip}"
    }
    ```

    > [!TIP]
    > O arquivo `outputs.tf` Ã© o arquivo que define as saÃ­das que serÃ£o exibidas apÃ³s a criaÃ§Ã£o da infraestrutura, nesse caso, o IP pÃºblico da instÃ¢ncia EC2.

9. Agora vamos criar o arquivo `variables.tf`, incluindo esse trecho abaixo nele:
    ```hcl
    variable "meu_ip_publico" {
        type        = string
        description = "EndereÃ§o IP pÃºblico para o Security Group SSH"
        # IMPORTANTE: Substitua pelo seu endereÃ§o IP pÃºblico
        # Para saber o seu IP pÃºblico, acesse https://www.whatismyip.com/
        default     = "203.0.113.25/32"
    }
    ```

    > [!TIP]
    > O arquivo `variables.tf` Ã© o arquivo que define as variÃ¡veis que serÃ£o usadas na infraestrutura, nesse caso, o IP pÃºblico para o Security Group SSH.

10. Boa! terminamos de criar todos os arquivos necessÃ¡rios para a criaÃ§Ã£o da infraestrutura na nuvem.

11. Agora vamos iniciar o fluxo de trabalho do Terraform para criar a infraestrutura na nuvem:
    ```bash
    terraform init
    terraform plan
    terraform apply
    ```

    > [!NOTE]
    > O comando `terraform init` inicializa o Terraform e baixa os providers necessÃ¡rios para a criaÃ§Ã£o da infraestrutura na nuvem.
    > O comando `terraform plan` cria um plano de execuÃ§Ã£o que mostra as alteraÃ§Ãµes que serÃ£o feitas na infraestrutura na nuvem.
    > O comando `terraform apply` aplica as configuraÃ§Ãµes definidas nos arquivos .tf e cria a infraestrutura na nuvem.

12. Se tudo rodar com sucesso, vocÃª verÃ¡ o IP pÃºblico da instÃ¢ncia EC2 e a URL do site provisionado, basta acessÃ¡-lo para ver o site estÃ¡ no ar.

13. Para destruir a infraestrutura na nuvem, execute o comando abaixo:
    ```bash
    terraform destroy
    ```

    > [!NOTE]
    > O comando `terraform destroy` destrÃ³i a infraestrutura na nuvem que foi criada pelo Terraform.
    > **RECOMENDADO**: Sempre que vocÃª criar uma infraestrutura na nuvem, certifique-se de destruÃ­-la quando nÃ£o estiver mais usando.

## Saiba mais

- [DocumentaÃ§Ã£o do Terraform](https://developer.hashicorp.com/terraform)
